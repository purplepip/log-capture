plugins {
  id "com.diffplug.gradle.spotless" version "3.16.0"
  id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "jacoco"
apply plugin: 'com.diffplug.gradle.spotless'

group = 'com.purplepip'
description = "Log capture"

ext {
  junitVersion      = "5.3.1"
  logbackVersion    = "1.2.3"
  slf4jVersion      = "1.7.25"
}
jacoco.toolVersion  = "0.8.2"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

configurations {
  deploy
}

dependencies {
  compile group: 'org.slf4j', name: 'slf4j-api', version:slf4jVersion
  compile group: 'ch.qos.logback', name: 'logback-classic', version:logbackVersion
  deploy group: 'io.packagecloud.maven.wagon', name: 'maven-packagecloud-wagon', version:"0.0.6"
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version:junitVersion
  testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version:junitVersion
}

test {
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true
  }
  finalizedBy jacocoTestReport
}

spotless {
  java {
    googleJavaFormat()
  }
}

repositories {
  jcenter()
}

uploadArchives {
  repositories.mavenDeployer {
    configuration = configurations.deploy
    def baseUrl = 'packagecloud+https://packagecloud.io/purplepip'
    def repositoryUrl = version.endsWith('SNAPSHOT') ? "${baseUrl}/snapshots" : "${baseUrl}/releases"
    repository(url: repositoryUrl) {
      authentication(password: System.env.PACKAGECLOUD_TOKEN ?: project.properties.'packagecloud.token')
    }
  }
}